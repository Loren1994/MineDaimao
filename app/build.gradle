apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'

android.sourceSets.main.java.srcDirs += 'build/generated/source/objectbox'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version
    defaultConfig {
        applicationId "com.example.loren.minesample"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
//            minifyEnabled true
//            shrinkResources true
//            zipAlignEnabled true
        }
    }
    sourceSets {
        main {
            proto {
                srcDirs 'src/main/proto'
                include '*.proto'
            }
            java.srcDirs = ['src/main/java', 'src/main/aidl']
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        pickFirst 'META-INF/maven/org.bytedeco.javacpp-presets/opencv/pom.properties'
        pickFirst 'META-INF/maven/org.bytedeco.javacpp-presets/opencv/pom.xml'
        pickFirst 'META-INF/maven/org.bytedeco.javacpp-presets/ffmpeg/pom.properties'
        pickFirst 'META-INF/maven/org.bytedeco.javacpp-presets/ffmpeg/pom.xml'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$android_support_version"
    implementation "androidx.legacy:legacy-support-v4:$android_support_version"
    implementation "androidx.recyclerview:recyclerview:$android_support_version"
    implementation "com.google.android.material:material:$android_support_version"
    implementation "androidx.cardview:cardview:$android_support_version"
    implementation "androidx.core:core-ktx:$android_support_version"
    implementation 'com.google.android:flexbox:0.3.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.facebook.fresco:fresco:0.13.0'
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'
    implementation 'com.balysv.materialmenu:material-menu:2.0.0'
    implementation 'pub.devrel:easypermissions:1.2.0'
    implementation 'com.orhanobut:logger:2.1.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation('com.github.VictorChow:kotlin-android-lib:1.2.2') {
        transitive = false
    }
    implementation "io.objectbox:objectbox-android:$objectboxVersion"
    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    kapt "io.objectbox:objectbox-processor:$objectboxVersion"
    implementation 'com.yanzhenjie:andserver:1.1.0'
    // 定义protobuf依赖，使用精简版
    implementation "com.google.protobuf:protobuf-lite:3.0.0"
    implementation('com.squareup.retrofit2:converter-protobuf:2.2.0') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"
    implementation 'org.bytedeco:javacv:1.4.1'
    implementation 'org.bytedeco.javacpp-presets:opencv:3.4.1-1.4.1:android-arm'
    implementation 'org.bytedeco.javacpp-presets:ffmpeg:3.4.2-1.4.1:android-arm'
//    implementation "com.android.support:palette-v7:$android_support_version"
//    implementation 'hanks.xyz:htextview-library:0.1.2'
//    implementation 'com.hulab.android:debugkit:1.1.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

androidExtensions {
    experimental = true
}
apply plugin: 'io.objectbox'
kotlin {
    experimental {
        coroutines "enable"
    }
}